
apply plugin: 'jacoco'

configurations {
	test {
		extendsFrom testImplementation
	}
}

dependencies {
	test sourceSets.test.output
}

dependencies {
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.hamcrest:hamcrest-core:1.3'
	testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

test {
	exclude '**/*IT.*'
}

plugins.withId( 'org.springframework.boot' ) {
	task itest( type: Test ) {
		dependsOn 'startDB'
		finalizedBy 'stopDB'

		check.dependsOn it
		mustRunAfter test
	
		include '**/*IT.*'
	}
}

task testReport(type: TestReport) {
	destinationDir = file("${rootProject.buildDir}/reports/tests")

	tasks.withType(Test) {
		reportOn it
	}
}

tasks.withType(Test) {
	ignoreFailures = gradle.startParameter.continueOnFailure

	systemProperty 'spring.profiles.active', name
	systemProperty 'APP_NAME', project.name
	systemProperty 'LOG_PATH', project.buildDir
	//systemProperty 'forceExecution', UUID.randomUUID().toString()

	testLogging.showStandardStreams = true
 	testLogging.exceptionFormat = 'full'

	reports.html.enabled = false

	check.dependsOn it
	testReport.dependsOn it

	doLast {
		jacocoTestReport.executionData it
	}
}

check.dependsOn testReport
//check.dependsOn jacocoTestReport

