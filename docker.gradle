import org.apache.tools.ant.filters.*

def dockerFile = file('data').exists() ? 'docker-compose-data.yml' : 'docker-compose.yml'

def httpPort = Integer.toString( portBase + 80 )
def jdbcPort = Integer.toString( portBase + 54 )

task 'docker-files'(type: Copy) {
	into file("${buildDir}/docker")

	from( rootProject.file('docker') ) {
		filter ReplaceTokens, tokens: [application: project.name, httpPort: httpPort, jdbcPort: jdbcPort]
	}
	from( file('data') ) {
		into 'data'
	}
	from( file("$buildDir/distributions") ) {
		include '*.tar'
	}
}

task 'docker-up'(type: Exec) {
	dependsOn 'docker-files'
	dependsOn 'assemble'

	executable 'docker-compose'
	workingDir file("${buildDir}/docker")

	args '-f', dockerFile
	args 'up', '--build', '-d'
}

task 'docker-stop'(type: Exec) {
	dependsOn 'docker-files'

	executable 'docker-compose'
	workingDir file("${buildDir}/docker")

	args '-f', dockerFile
	args 'stop'
}

task 'docker-down'(type: Exec) {
	dependsOn 'docker-files'

	executable 'docker-compose'
	workingDir file("${buildDir}/docker")

	args '-f', dockerFile
	args 'down'
}

bootRun {
	systemProperty "database.port", jdbcPort
}

task startDB(type: Exec) {
	executable 'docker'

	args 'run', '--name', "${project.name}-db"
	args '-d', '--rm'
	args '-e', "POSTGRES_USER=$project.name"
	args '-e', "POSTGRES_PASSWORD=$project.name"
	args '-e', "POSTGRES_DB=$project.name"
	args '-p', "${jdbcPort}:5432"
	args 'postgres:11'
}

task stopDB(type: Exec) {
	executable 'docker'

	args 'stop', "${project.name}-db"
}
