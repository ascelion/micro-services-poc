
final def dataDir        = file 'data'
final def destination    = file "$buildDir/distributions"
final def jdbcPort       = docker.configuration.portBase + 5432

bootRun {
	systemProperty "database.port", jdbcPort
}

task startDB(type: Exec) {
	onlyIf { docker.configuration.hasDatabase }

	executable 'docker'

	args 'run', '--name', "${project.name}-db-dev"
	args '-d', '--rm'
	args '-e', "POSTGRES_USER=$project.name"
	args '-e', "POSTGRES_PASSWORD=$project.name"
	args '-e', "POSTGRES_DB=$project.name"
	args '-p', "${jdbcPort}:5432"
	args 'postgres:11.5'
}

task stopDB(type: Exec) {
	onlyIf { docker.configuration.hasDatabase }

	executable 'docker'

	ignoreExitValue true

	args 'stop', "${project.name}-db-dev"
}

docker {
	outputDirectory destination
	templates rootProject.fileTree( 'docker' )

	configuration {
		hasImportData = dataDir.exists()
	}
}

task dockerFilesData(type: Copy) {
	into destination

	from( file('data') ) {
		into 'data'
	}

	onlyIf { docker.configuration.hasImportData }
}

dockerFiles {
	dependsOn dockerFilesData
}

